definition {
	macro addNewChangeList {
		Click(locator1 = "Button#PLUS");

		Type(locator1 = "ChangeList#CHANGE_LIST_NAME", value1 = "${changeListName}");

		Type(locator1 = "ChangeList#CHANGE_LIST_DESCRIPTION", value1 = "${changeListDescription}");

		Button.clickSave();
	}

	macro cancelChangeListCreation {
		Click(locator1 = "Button#PLUS");

		Button.clickCancel();

		AssertTextEquals(
			locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
			value1 = "Select Change List"
		);
	}

	macro disableChangeList {
		Uncheck.uncheckToggleSwitch(
			locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH"
		);

		if (isSet(gotoOverview)) {
			Click(locator1 = "ChangeListSettings#SAVE_AND_GO_TO_OVERVIEW_BUTTON");
		}
		else {
			Click(locator1 = "Button#SAVE");
		}

		Alert.viewSuccessMessage();
	}

	macro enableChangeList {
		Check.checkToggleSwitch(
			locator1 = "ChangeListSettings#CHANGE_LISTS_TOGGLE_SWITCH"
		);

		if (isSet(gotoOverview)) {
			Click(locator1 = "ChangeListSettings#SAVE_AND_GO_TO_OVERVIEW_BUTTON");
		}
		else {
			Click(locator1 = "Button#SAVE");
		}

		Alert.viewSuccessMessage();
	}

	macro searchForChangeList {
		Search.searchCP(searchTerm = "${changeListName}");

		var key_changeListName = "${changeListName}";

		AssertTextPresent(
			locator1 = "ChangeList#CHANGE_LIST_TABLE_ENTRY_NAME",
			value1 = "${changeListName}"
		);
	}

	macro selectChangeList {
		Click(locator1 = "ChangeList#SELECT_CHANGE_LIST_DROPDOWN");

		var key_changeListName = "${changeListName}";

		if ("${changeListName}" == "Select Change List") {
			Click(locator1 = "ChangeList#SELECT_CHANGE_LIST");

			AssertTextEquals(
				locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
				value1 = "Select Change List"
			);
		}
		else {
			Click(
				locator1 = "ChangeList#SELECT_CHANGE_LIST_VALUE",
				key_changeListName = "${changeListName}"
			);

			AssertTextEquals(
				locator1 = "ChangeList#OVERVIEW_PAGE_TITLE",
				value1 = "Overview"
			);
		}
	}

	macro selectChangeListMenuItem {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "${menuItem}",
			rowEntry = "${rowEntry}"
		);
	}
}